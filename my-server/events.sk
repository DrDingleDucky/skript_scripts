variables:
    {gold::%player%} = 0
    {kills::%player%} = 0
    {status::timer::%player%} = 0

function clear(p: player):
    execute console command "/minecraft:clear %{_p}%"
    execute console command "/minecraft:effect clear %{_p}%"

function kit_1(p: player):
    clear({_p})
    set slot 40 of {_p} to unbreakable shield
    set slot 0 of {_p} to unbreakable iron sword
    set slot 1 of {_p} to unbreakable iron axe
    set helmet of {_p} to unbreakable iron helmet
    set chestplate of {_p} to unbreakable iron chestplate
    set leggings of {_p} to unbreakable iron leggings
    set boots of {_p} to unbreakable iron boots
    heal {_p}

function kit_2(p: player):
    clear({_p})
    execute console command "/effect give %{_p}% minecraft:speed infinite 1 false"
    set slot 40 of {_p} to unbreakable shield
    set slot 0 of {_p} to unbreakable diamond sword
    set helmet of {_p} to unbreakable leather helmet
    set chestplate of {_p} to unbreakable iron chestplate
    set leggings of {_p} to unbreakable leather leggings
    set boots of {_p} to unbreakable leather boots
    heal {_p}

function kit_3(p: player):
    clear({_p})
    set slot 40 of {_p} to unbreakable shield
    set slot 0 of {_p} to unbreakable iron sword
    set slot 1 of {_p} to unbreakable bow
    set slot 2 of {_p} to 24 arrows
    set helmet of {_p} to unbreakable iron helmet
    set chestplate of {_p} to unbreakable iron chestplate
    set leggings of {_p} to unbreakable iron leggings
    set boots of {_p} to unbreakable iron boots
    heal {_p}

on join:
    teleport player to location(0.5, -60, 0.5, world "main")
    while player is online:
        if player is in world "pvp":
            if {status::timer::%player%} > 0:
                set line 1 of player's scoreboard to "&cIn Combat <gold>(%{status::timer::%player%}%)"
            else:
                set line 1 of player's scoreboard to "<light green>Not In Combat"
            set line 2 of player's scoreboard to ""
            set line 3 of player's scoreboard to "Kill Streak: %{kills::%player%}%"
            set line 4 of player's scoreboard to "Gold: <gold>%{gold::%player%}%g"
            set line 5 of player's scoreboard to ""
            set title of player's scoreboard to "<orange>Testing Server"
        subtract 1 from {status::timer::%player%}
        wait 1 second

on quit:
    delete {kills::%player%}
    delete {status::timer::%player%}

on damage:
    if victim is in world "main":
        cancel event
    else if victim is in world "pvp":
        if y coordinate of victim is above -44:
            cancel event
        else:
            set {status::timer::%victim%} to 5
            set {status::timer::%attacker%} to 5

on death:
    if victim is a player:
        add 1 to {kills::%attacker%}
        add 3 to {gold::%attacker%}
        set {kills::%victim%} to 0
        set {status::timer::%victim%} to 0
        clear(victim)

on rightclick on sign:
    # lobby
    if line 2 of block is "PvP Arena":
        teleport player to location(0.5, -43, 0.5, world "pvp")
    else if line 2 of block is "Parkour":
        send "Parkour Comming Soon..." to player
    else if line 2 of block is "CTF":
        send "CTF Comming Soon..." to player
    # pvp arena
    else if line 2 of block is "Lobby":
        teleport player to location(0.5, -60, 0.5, world "main")
    else if line 2 of block is "Knight":
        kit_1(player)
    else if line 2 of block is "Ninja":
        kit_2(player)
    else if line 2 of block is "Archer":
        kit_3(player)

# stops player from shooting bows in lobby of "pvp"
on shoot:
    if shooter is in world "pvp":
        if projectile is an arrow:
            if y coordinate of projectile is above -44:
                cancel event

# set respawn location to the right world
on respawn:
    if player is in world "main":
        set respawn location to location(0.5, -60, 0.5, world "main")
    else if player is in world "pvp":
        set respawn location to location(0.5, -43, 0.5, world "pvp")

# removes hunger
on food bar change:
    cancel event

# stop player from dropping items
on drop:
    cancel event

# can't break blocks in survival
on break:
    if gamemode of player is survival:
        cancel event     
