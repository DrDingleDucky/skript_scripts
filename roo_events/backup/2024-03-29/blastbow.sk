on skript load:
    if {blastbow.cooldown} is not set:
        set {blastbow.cooldown} to "45 seconds" parsed as timespan
    if {blastbow.dmg} is not set:
        set {blastbow.dmg} to 5

on projectile hit:
    if metadata value "BParrow" of event-projectile is true:
        set metadata value "BParrow" of event-projectile to false
        draw 50 of dust using dustOption(red, 3) at event-projectile with offset vector(2, 2, 2) with force
        create an explosion of force {blastbow.dmg} at the event-projectile
        play sound "entity.iron_golem.repair" with volume 5 with pitch 0.3 to all players at event-projectile
        delete event-projectile

on shoot:
    if byte tag "BPbow" of nbt compound of shooter's tool is 1:
        if {bbc::%shooter's uuid%} is not set:
            set {bbc::%shooter's uuid%} to now
            set item cooldown of shooter's tool for shooter to {blastbow.cooldown}
            set metadata value "BParrow" of event-projectile to true
            play sound "entity.zombie.attack_iron_door" with volume 0.5 with pitch 0.7 to all players at shooter's location
            loop 30 times:
                wait 0.05 seconds
                if metadata value "BParrow" of event-projectile is true:
                    draw 5 of flame at event-projectile with offset vector(0.2, 0.2, 0.2) with extra 0
                else:
                    stop
            stop
        set {_bbt} to difference between {bbc::%shooter's uuid%} and now
        if {_bbt} is less than {blastbow.cooldown}:
            cancel event
            set {_bbc} to {blastbow.cooldown}
            subtract {_bbt} from {_bbc}
            send "&cYou must wait %rounded seconds of {_bbc}% seconds before using this again!" to shooter
            stop
        else:
            set {bbc::%shooter's uuid%} to now
            set item cooldown of shooter's tool for shooter to {blastbow.cooldown}
            set metadata value "BParrow" of event-projectile to true
            play sound "entity.zombie.attack_iron_door" with volume 0.5 with pitch 0.7 to all players at shooter's location
            loop 30 times:
                wait 0.05 seconds
                if metadata value "BParrow" of event-projectile is true:
                    draw 5 of flame at event-projectile with offset vector(0.2, 0.2, 0.2) with extra 0
                else:
                    stop
            stop

on swap hand items:
    if byte tag "BPbow" of nbt compound of player's tool is 1:
        cancel event
        send "&cYou can't put this item in your off hand!" to player

on inventory slot change:
    if event-slot is 40:
        if byte tag "BPbow" of nbt compound of event-item is 1:
            set slot 40 of player to air
            set cursor slot of player to event-item
            send "&cYou can't put this item in your off hand!" to player